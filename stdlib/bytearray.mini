//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

type ByteArray = struct { 
    size: uint,
    sliceOffset: uint,
    contents: buffer,
    copy: bool,
}

// bytearray_new makes a new ByteArray
// it will initially have size zero,
//        reads beyond the end will return zero,
//        size will expand to fit all writes,
//        and the capacity arg is a hint about how big it will get
public func bytearray_new(ignored: uint) -> ByteArray {
    return struct {
        size: 0,
        sliceOffset: 0,
        contents: newbuffer(0),
        copy: false,
    };
}

type MarshalledBytes = struct {
    nbytes: uint,
    contents: buffer,
}

public func marshalledBytes_firstByte(mb: MarshalledBytes) -> uint {
    let nbytes = mb.nbytes;
    let contents = mb.contents;
    if (nbytes < 1) {
        return 0;
    }
    return getbuffer8(contents, 0);
}

public func marshalledBytes_hash(mb: MarshalledBytes) -> bytes32 {
    // This is an easy-to-compute cryptographic hash of a MarshalledBytes
    // It is collision-free, assuming that the underlying hash operation is collision-free.
    return hash(mb);
}

// bytearray_unmarshalBytes unmarshals a bytestack object that we got from L1
//        returns a new bytearray holding the unmarshaled bytes
public func bytearray_unmarshalBytes(inBytes: MarshalledBytes) -> ByteArray {
    return struct {
        size: inBytes.nbytes,
        sliceOffset: 0,
        contents: inBytes.contents,
        copy: false,
    };
}

public func bytearray_marshalFull(ba: ByteArray) -> MarshalledBytes {
    return struct {
        nbytes: ba.size,
        contents: bytearray_copy(make(ba), ba.sliceOffset, bytearray_new(0), 0, ba.size).contents,
    };
}

// bytearray_size gets the size of a ByteArray
public func bytearray_size(ba: ByteArray) -> uint {
    return ba.size;
}

public func bytearray_buffer(ba: ByteArray) -> buffer {
    return ba.contents;
}

public func bytearray_slice(ba: ByteArray) -> uint {
    return ba.sliceOffset;
}

// bytearray_getByte reads one byte from a ByteArray
public func bytearray_getByte(ba: ByteArray, offset: uint) -> uint {
    if (offset >= ba.size) {
        return 0;
    }
    offset = offset + ba.sliceOffset;
    return getbuffer8(ba.contents, offset);
}

// bytearray_get64 reads a chunk of 8 bytes from a ByteArray
public func bytearray_get64(ba: ByteArray, offset: uint) -> uint {
    if (offset >= ba.size) {
        return 0;
    }
    offset = offset + ba.sliceOffset;
    return getbuffer64(ba.contents, offset);
}

// bytearray_get256 reads a chunk of 32 bytes from a ByteArray
public func bytearray_get256(ba: ByteArray, offset: uint) -> uint {
    if (offset >= ba.size) {
        return 0;
    }
    offset = offset + ba.sliceOffset;
    return getbuffer256(ba.contents, offset);
}

func make(cont: ByteArray) -> ByteArray {
    return struct {
        size: cont.size + cont.sliceOffset + 32,
        sliceOffset: 0,
        contents: cont.contents,
        copy: true,
    };
}

public func bytearray_wrap(cont: buffer, size: uint) -> ByteArray {
    return struct {
        size: size,
        sliceOffset: 0,
        contents: cont,
        copy: false,
    };
}

// bytearray_setbyte writes one byte to a ByteArray, returns the resulting ByteArray
public func bytearray_setByte(ba: ByteArray, offset: uint, value: uint) -> ByteArray {
    // if (ba.copy) { ba = bytearray_copy(make(ba), ba.sliceOffset, bytearray_new(0), 0, ba.size); }
    if (offset >= ba.size) {
        ba = ba with { size: offset+1 };
    }
    offset = offset + ba.sliceOffset;
    return ba with { contents: setbuffer8(ba.contents, offset, value) };
}

// bytearray_set64 writes an 8-byte chunk to a ByteArray, returning the resulting ByteArray
public func bytearray_set64(ba: ByteArray, offset: uint, value: uint) -> ByteArray {
    if (offset+8 > ba.size) {
        ba = ba with { size: offset+8 };
    }
    offset = offset + ba.sliceOffset;
    return ba with { contents: setbuffer64(ba.contents, offset, value) };
}

// bytearray_set256 writes a 32-byte chunk to a ByteArray, returning the resulting ByteArray
public func bytearray_set256(ba: ByteArray, offset: uint, value: uint) -> ByteArray {
    if (offset+32 > ba.size) {
        ba = ba with { size: offset+32 };
    }
    offset = offset + ba.sliceOffset;
    return ba with { contents: setbuffer256(ba.contents, offset, value) };
}

// bytearray_copy copies bytes from one ByteArray to another, 
//       returning the updated destination ByteArray
public func bytearray_copy(
    from: ByteArray, 
    fromOffset: uint,
    to: ByteArray,
    toOffset: uint,
    nbytes: uint,
) -> ByteArray {
    // TODO: optimize this to exploit alignment and speed up the endgame
    // debug(nbytes);
    if (to.size < toOffset + nbytes) {
        to = to with { size: toOffset + nbytes };
    }
    fromOffset = fromOffset + from.sliceOffset;
    toOffset = toOffset + to.sliceOffset;
    let toContents = to.contents;
    let fromContents = from.contents;
    while (nbytes >= 32) {
        let val = getbuffer256(fromContents, fromOffset);
        toContents = setbuffer256(toContents, toOffset, val);
        fromOffset = 32+fromOffset;
        toOffset = 32+toOffset;
        nbytes = nbytes-32;
    }
    while (nbytes > 0) {
        let val = getbuffer8(fromContents, fromOffset);
        toContents = setbuffer8(toContents, toOffset, val);
        fromOffset = 1+fromOffset;
        toOffset = 1+toOffset;
        nbytes = nbytes-1;
    }
    return to with { contents: toContents };
}

public func bytearray_extract(from: ByteArray, offset: uint, nbytes: uint) -> ByteArray {
    return struct { size: nbytes, sliceOffset: from.sliceOffset + offset, contents: from.contents, copy: true, };
    // return bytearray_copy(from, offset, bytearray_new(0), 0, nbytes);
}

