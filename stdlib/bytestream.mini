//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

import type ByteArray;

import func bytearray_new(unused: uint) -> ByteArray;
import func bytearray_size(ba: ByteArray) -> uint;
import func bytearray_slice(ba: ByteArray) -> uint;
import func bytearray_buffer(ba: ByteArray) -> buffer;
import func bytearray_getByte(ba: ByteArray, offset: uint) -> uint;
import func bytearray_get64(ba: ByteArray, offset: uint) -> uint;
import func bytearray_get256(ba: ByteArray, offset: uint) -> uint;
import func bytearray_extract(ba: ByteArray, offset: uint, nbytes: uint) -> ByteArray;
import func bytearray_wrap_slice(ba: buffer, offset: uint, nbytes: uint) -> ByteArray;
import func bytearray_copy(from: ByteArray, fromOffset: uint, to: ByteArray, toOffset: uint, nbytes: uint) -> ByteArray;

type ByteStream = struct {
    buf: buffer,
    capacity: uint,
    slice: uint,
    currentOffset: uint,
}

public func bytestream_new(contents: ByteArray) -> ByteStream {
    return struct {
        buf: bytearray_buffer(contents),
        capacity: bytearray_size(contents),
        slice: bytearray_slice(contents),
        currentOffset: 0,
    };
}

public func bytestream_atEof(bs: ByteStream) -> bool {
    return bs.currentOffset >= bs.capacity;
}

public func bytestream_bytesReadSoFar(bs: ByteStream) -> uint {
    return bs.currentOffset;
}

public func bytestream_bytesRemaining(bs: ByteStream) -> uint {
    if (bs.currentOffset >= bs.capacity) {
        return 0;
    } else {
        return bs.capacity - bs.currentOffset;
    }
}

public func bytestream_skipBytes(bs: ByteStream, nbytes: uint) -> option<ByteStream> {
    let newOffset = bs.currentOffset + nbytes;
    if (newOffset <= bs.capacity) {
        return Some(bs with { currentOffset: newOffset });
    } else {
        return None;
    }
}

public func bytestream_truncate(bs: ByteStream, size: uint) -> ByteStream {
    if (size < bs.capacity) {
        if (size < bs.currentOffset) {
            return bs with { capacity: bs.currentOffset };
        } else {
            return bs with { capacity: size };
        }
    } else {
        return bs;
    }
}

public func bytestream_getByte(bs: ByteStream) -> option<(ByteStream, uint)> {
    if (bs.currentOffset >= bs.capacity) {
        return None;
    } else {
        return Some((
            bs with { currentOffset: bs.currentOffset+1 },
            getbuffer8(bs.buf, bs.currentOffset+bs.slice)
        ));
    }
}

public func bytestream_get64(bs: ByteStream) -> option<(ByteStream, uint)> {
    if (bs.currentOffset+8 > bs.capacity) {
        return None;
    } else {
        return Some((
            bs with { currentOffset: bs.currentOffset+8 },
            getbuffer64(bs.buf, bs.currentOffset+bs.slice)
        ));
    }
}

public func bytestream_get256(bs: ByteStream) -> option<(ByteStream, uint)> {
    if (bs.currentOffset+32 > bs.capacity) {
        return None;
    } else {
        return Some((
            bs with { currentOffset: bs.currentOffset+32 },
            getbuffer256(bs.buf, bs.currentOffset+bs.slice)
        ));
    }
}

public func bytestream_getN(bs: ByteStream, nbytes: uint) -> option<(ByteStream, ByteArray)> {
    if (bs.currentOffset + nbytes > bs.capacity) {
        return None;
    }

    return Some((
        bs with { currentOffset: bs.currentOffset + nbytes },
        bytearray_wrap_slice(bs.buf, bs.currentOffset + bs.slice, nbytes)
    ));
}

public func bytestream_getRemainingBytes(bs: ByteStream) -> ByteArray {
    if (bs.capacity > bs.currentOffset) {
        return bytearray_wrap_slice(bs.buf, bs.currentOffset + bs.slice, bs.capacity - bs.currentOffset);
        // return bytearray_extract( bs.contents, bs.currentOffset, bs.capacity - bs.currentOffset );
    } else {
        return bytearray_new(0);
    }
}
