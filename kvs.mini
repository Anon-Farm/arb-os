
import func builtin_arrayNew(sz:uint) anytype;
import func builtin_arrayGet(arr: []anytype, idx: uint) anytype;
import func builtin_arraySet(arr: []anytype, idx: uint, val: anytype) []anytype;
import func builtin_arraySwap(arr: []anytype, idx: uint, val: anytype) ([]anytype, anytype);


type ShtItem = struct {
	key: bytes32,
	value: anytype,
}

type SimpleHashtable = struct {
	size: uint,
	contents: []ShtItem,
}

func simpleHashtable_new() SimpleHashtable {
	return struct {
		size: 8,
		contents: unsafecast(newarray(8, anytype), []ShtItem),
	};
}

func simpleHashtable_get(table: SimpleHashtable, key: bytes32) anytype {
	let slot = uint(key) % table.size;
	let item = table.contents[slot];
	if ((item == null) || (key != item.key)) {
		return null;
	} else {
		return item.value;
	}
}

func simpleHashtable_set(table:SimpleHashtable, key: bytes32, val: anytype) SimpleHashtable {
	let slot = uint(key) % table.size;
	let newitem = struct { key: key, value: val, };
	return table with { contents: table.contents with {[slot] = newitem} };
}

func simpleHashtable_swap(
	table: SimpleHashtable, 
	key: bytes32, 
	val: anytype
) (SimpleHashtable, ShtItem) {
	let sres = unsafecast(
		builtin_arraySwap(
			table.contents, 
			uint(key) % table.size, 
			struct {
				key: key,
				value: val,
			}
		),
		([]ShtItem, ShtItem)
	);
	return (
		table with { contents: sres.0 },
		sres.1,
	);
}

func simpleHashtable_expand(table: SimpleHashtable) SimpleHashtable {
	let newTable = struct {
		size: 2*table.size,
		contents: unsafecast(newarray(8, anytype), []ShtItem),
	};
	let i = 0;
	while (i < table.size) {
		let oldItem = table.contents[i];
		if (oldItem != null) {
			newTable = simpleHashtable_set(newTable, oldItem.key, oldItem.value);
		}
		i = i+1;
	}
	return newTable;
}

type KeyValueStore = [2]SimpleHashtable

public func kvs_new() KeyValueStore {
	return newfixedarray(2, simpleHashtable_new()) with { [1] = simpleHashtable_new() };
}

public func kvs_get(kvs: KeyValueStore, key: anytype) anytype {
	let val1 = simpleHashtable_get(
		kvs[0], 
		hash(bytes32(0), hash(key))
	);
	if (val1 != null) {
		return val1;
	}
	return simpleHashtable_get(
		kvs[1], 
		hash(bytes32(1), hash(key))
	);
}

public func kvs_set(kvs: KeyValueStore, key: anytype, value:anytype) KeyValueStore {
	let which = 0;
	let retries = 0;
	let item = struct {
		key: hash(key),
		value: value,
	};
	while (retries < 5) {
		let res = unsafecast(
			simpleHashtable_swap(kvs[which], item.key, item.value),
			([]ShtItem, ShtItem)
		);
		kvs = kvs with { [which] = res.0 };
		if (res.1 == null) {
			return kvs;
		}
		item = res.1;
		which = 1-which;
		retries = 1+retries;
	}
	return kvs_set(
		kvs with { [0] = simpleHashtable_expand(kvs[0]) } 
	        with { [1] = simpleHashtable_expand(kvs[1]) },
		item.key,
		item.value
	);
}
