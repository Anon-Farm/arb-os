//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

use accounts::Account;
use evmCallStack::EvmCallFrame;
use evmLogs::EvmLogs;
use std::bytearray::ByteArray;
use std::bytestream::ByteStream;
use std::bytearray::MarshalledBytes;
use std::stack::Stack;
use std::storageMap::StorageMap;

use chainParameters::chainParams_chainId;
use errorHandler::errorHandler;

use evmCallStack::evmCallStack_topFrame;
use evmCallStack::evmCallStack_oldestCallFrame;
use evmCallStack::evmCallStack_setAccount;
use evmCallStack::evmCallStack_getAccount;
use evmCallStack::evmCallStack_getTopFrameReturnData;
use evmCallStack::evmCallStack_getTopFrameMemoryOrDie;
use evmCallStack::evmCallStack_setTopFrameMemory;
use evmCallStack::evmCallStack_setTopFrameStorageCell;
use evmCallStack::evmCallStack_addEvmLogToCurrent;
use evmCallStack::evmCallStack_selfDestructCurrentAccount;
use evmCallStack::evmCallStack_doCall;
use evmCallStack::evmCallStack_returnFromCall;
use evmCallStack::evmCallStack_callHitError;
use evmCallStack::evmCallStack_runningCodeFromAccount;
use evmCallStack::snapshotAuxStack;

use evmCallStack::evmCallFrame_runningAsAccount;
use evmCallStack::evmCallFrame_runningCodeFromAccount;
use evmCallStack::evmCallFrame_getAccount;
use evmCallStack::evmCallFrame_getCaller;
use evmCallStack::evmCallFrame_getMemory;
use evmCallStack::evmCallFrame_getResumeCodePoint;
use evmCallStack::evmCallFrame_getCallvalue;
use evmCallStack::evmCallFrame_getCalldata;
use evmCallStack::evmCallFrame_getEvmLogs;
use evmCallStack::evmCallFrame_shouldRevertOnStorageWrite;

use accounts::account_getAddress;
use accounts::account_getCodeSize;
use accounts::account_getBalance;
use accounts::account_getCode;
use accounts::account_getStorageCell;
use accounts::account_getAllStorage;
use accounts::account_getEvmJumpTable;
use accounts::account_fetchAndIncrSeqNum;
use accounts::account_setContractInfo;
use accounts::account_isEmpty;
use accounts::account_hasContract;
use accounts::pristineAccount;

use std::storageMap::storageMap_new;

use evmCallStack::evmCallStack_currentTimestamp;
use evmCallStack::evmCallStack_currentBlockNumber;

use evmLogs::evmlogs_empty;

use std::bytearray::bytearray_new;
use std::bytearray::bytearray_size;
use std::bytearray::bytearray_getByte;
use std::bytearray::bytearray_setByte;
use std::bytearray::bytearray_get256;
use std::bytearray::bytearray_set256;
use std::bytearray::bytearray_extract;
use std::bytearray::bytearray_copy;
use std::bytearray::bytearray_marshalFull;
use std::bytestream::bytestream_new;

use std::stack::stack_pop;
use std::stack::stack_isEmpty;
use std::stack::stack_discardDeepestItems;

use codeSegment::translateEvmCodeSegment;

use std::keccak::keccak256;
use std::rlp::keccakOfRlpEncodedUintPair;

const opNum_stop = 0;
const opNum_sha3 = 1;
const opNum_address = 2;
const opNum_balance = 3;
const opNum_selfbalance = 4;
const opNum_origin = 5;
const opNum_caller = 6;
const opNum_callvalue = 7;
const opNum_calldataload = 8;
const opNum_calldatasize = 9;
const opNum_calldatacopy = 10;
const opNum_codesize = 11;
const opNum_codecopy = 12;
const opNum_extcodesize = 13;
const opNum_extcodecopy = 14;
const opNum_extcodehash = 15;
const opNum_returndatasize = 16;
const opNum_returndatacopy = 17;
const opNum_timestamp = 18;
const opNum_number = 19;
const opNum_msize = 20;
const opNum_mload = 21;
const opNum_mstore = 22;
const opNum_mstore8 = 23;
const opNum_sload = 24;
const opNum_sstore = 25;
const opNum_getjumpaddr = 26;
const opNum_msize = 27;
const opNum_log0 = 28;
const opNum_log1 = 29;
const opNum_log2 = 30;
const opNum_log3 = 31;
const opNum_log4 = 32;
const opNum_call = 33;
const opNum_callcode = 34;
const opNum_delegatecall = 35;
const opNum_staticcall = 36;
const opNum_revert = 37;
const opNum_revert_knownPc = 38;
const opNum_return = 39;
const opNum_selfdestruct = 40;
const opNum_create = 41;
const opNum_create2 = 42;
const opNum_chainId = 43;
const num_opNums = 44;

var evmOpJumpTable: [constval(num_opNums)]impure func();

public impure func evmOps_init() {
    evmOpJumpTable = unsafecast<[constval(num_opNums)]impure func()>(newfixedarray(constval(num_opNums)));
    evmOps_init_makeOne(constval(opNum_stop), unsafecast<impure func()>(evmOp_stop));
    evmOps_init_makeOne(constval(opNum_sha3), unsafecast<impure func()>(evmOp_sha3));
    evmOps_init_makeOne(constval(opNum_address), unsafecast<impure func()>(evmOp_address));
    evmOps_init_makeOne(constval(opNum_balance), unsafecast<impure func()>(evmOp_balance));
    evmOps_init_makeOne(constval(opNum_selfbalance), unsafecast<impure func()>(evmOp_selfbalance));
    evmOps_init_makeOne(constval(opNum_origin), unsafecast<impure func()>(evmOp_origin));
    evmOps_init_makeOne(constval(opNum_caller), unsafecast<impure func()>(evmOp_caller));
    evmOps_init_makeOne(constval(opNum_callvalue), unsafecast<impure func()>(evmOp_callvalue));
    evmOps_init_makeOne(constval(opNum_calldataload), unsafecast<impure func()>(evmOp_calldataload));
    evmOps_init_makeOne(constval(opNum_calldatasize), unsafecast<impure func()>(evmOp_calldatasize));
    evmOps_init_makeOne(constval(opNum_calldatacopy), unsafecast<impure func()>(evmOp_calldatacopy));
    evmOps_init_makeOne(constval(opNum_codesize), unsafecast<impure func()>(evmOp_codesize));
    evmOps_init_makeOne(constval(opNum_codecopy), unsafecast<impure func()>(evmOp_codecopy));
    evmOps_init_makeOne(constval(opNum_extcodesize), unsafecast<impure func()>(evmOp_extcodesize));
    evmOps_init_makeOne(constval(opNum_extcodecopy), unsafecast<impure func()>(evmOp_extcodecopy));
    evmOps_init_makeOne(constval(opNum_extcodehash), unsafecast<impure func()>(evmOp_extcodehash));
    evmOps_init_makeOne(constval(opNum_returndatasize), unsafecast<impure func()>(evmOp_returndatasize));
    evmOps_init_makeOne(constval(opNum_returndatacopy), unsafecast<impure func()>(evmOp_returndatacopy));
    evmOps_init_makeOne(constval(opNum_timestamp), unsafecast<impure func()>(evmOp_timestamp));
    evmOps_init_makeOne(constval(opNum_number), unsafecast<impure func()>(evmOp_number));
    evmOps_init_makeOne(constval(opNum_msize), unsafecast<impure func()>(evmOp_msize));
    evmOps_init_makeOne(constval(opNum_mload), unsafecast<impure func()>(evmOp_mload));
    evmOps_init_makeOne(constval(opNum_mstore), unsafecast<impure func()>(evmOp_mstore));
    evmOps_init_makeOne(constval(opNum_mstore8), unsafecast<impure func()>(evmOp_mstore8));
    evmOps_init_makeOne(constval(opNum_sload), unsafecast<impure func()>(evmOp_sload));
    evmOps_init_makeOne(constval(opNum_sstore), unsafecast<impure func()>(evmOp_sstore));
    evmOps_init_makeOne(constval(opNum_getjumpaddr), unsafecast<impure func()>(evmOp_getjumpaddr));
    evmOps_init_makeOne(constval(opNum_msize), unsafecast<impure func()>(evmOp_msize));
    evmOps_init_makeOne(constval(opNum_log0), unsafecast<impure func()>(evmOp_log0));
    evmOps_init_makeOne(constval(opNum_log1), unsafecast<impure func()>(evmOp_log1));
    evmOps_init_makeOne(constval(opNum_log2), unsafecast<impure func()>(evmOp_log2));
    evmOps_init_makeOne(constval(opNum_log3), unsafecast<impure func()>(evmOp_log3));
    evmOps_init_makeOne(constval(opNum_log4), unsafecast<impure func()>(evmOp_log4));
    evmOps_init_makeOne(constval(opNum_call), unsafecast<impure func()>(evmOp_call));
    evmOps_init_makeOne(constval(opNum_callcode), unsafecast<impure func()>(evmOp_callcode));
    evmOps_init_makeOne(constval(opNum_delegatecall), unsafecast<impure func()>(evmOp_delegatecall));
    evmOps_init_makeOne(constval(opNum_staticcall), unsafecast<impure func()>(evmOp_staticcall));
    evmOps_init_makeOne(constval(opNum_revert), unsafecast<impure func()>(evmOp_revert));
    evmOps_init_makeOne(constval(opNum_revert_knownPc), unsafecast<impure func()>(evmOp_revert_knownPc));
    evmOps_init_makeOne(constval(opNum_return), unsafecast<impure func()>(evmOp_return));
    evmOps_init_makeOne(constval(opNum_selfdestruct), unsafecast<impure func()>(evmOp_selfdestruct));
    evmOps_init_makeOne(constval(opNum_create), unsafecast<impure func()>(evmOp_create));
    evmOps_init_makeOne(constval(opNum_create2), unsafecast<impure func()>(evmOp_create2));
    evmOps_init_makeOne(constval(opNum_chainId), unsafecast<impure func()>(chainParams_chainId));
}

impure func evmOps_init_makeOne(i: uint, f: impure func()) {
    evmOpJumpTable = evmOpJumpTable with { [i] = f };
}

public impure func evmOps_trampoline(index: uint) {
    asm(evmOpJumpTable[index],) {
        auxpop pop   // discard this function's call frame
        auxpop
        swap1
        // stack holds: funcPtr returnAddr
        jump
    };
}


// Any function with a name like evmOp_XXX is designed to implement the XXX instruction in EVM.
// Compiled EVM code will call that function to implement the XXX instruction.
// Most of the functions are fairly simple, and access the callframe or similar information.

public impure func evmOp_stop() {
    // return from the current call, with no calldata
    evmOp_return(0, 0);
}

public impure func evmOp_sha3(baseMemAddr: uint, nbytes: uint) -> bytes32 {
    return keccak256(evmCallStack_getTopFrameMemoryOrDie(), baseMemAddr, nbytes);
}

public impure func evmOp_address() -> address {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return account_getAddress(evmCallFrame_runningAsAccount(topFrame));
    }

    evm_runtimePanic(0);
    return address(0);
}

public impure func evmOp_balance(addrAsUint: uint) -> uint {
    let addr = address(addrAsUint);  // truncates if necessary
    if let Some(topFrame) = evmCallStack_topFrame() {
        return account_getBalance(
            evmCallFrame_getAccount(topFrame, addr)
        );
    } else {
        evm_runtimePanic(1);
        return 0;
    }
}

public impure func evmOp_selfbalance() -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return account_getBalance(
            evmCallFrame_runningAsAccount(topFrame)
        );
    } else {
        evm_runtimePanic(2);
        return 0;
    }
}
    
public impure func evmOp_origin() -> address {
    if let Some(oldie) = evmCallStack_oldestCallFrame() {
        return account_getAddress(evmCallFrame_runningAsAccount(oldie));
    }

    evm_runtimePanic(3);
    return address(0);
}
    
public impure func evmOp_caller() -> address {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return evmCallFrame_getCaller(topFrame);
    }

    evm_runtimePanic(4);
    return address(0);
}

public impure func evmOp_callvalue() -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return evmCallFrame_getCallvalue(topFrame);
    } else {
        evm_runtimePanic(5);
        return 0;
    }
}

public impure func evmOp_calldataload(offset: uint) -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return bytearray_get256(evmCallFrame_getCalldata(topFrame), offset);
    }

    evm_runtimePanic(6);
    return 0;
}

public impure func evmOp_calldatasize() -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return bytearray_size(evmCallFrame_getCalldata(topFrame));
    } else {
        evm_runtimePanic(7);
        return 0;
    }
}

public impure func evmOp_calldatacopy(memAddr: uint, calldataOffset: uint, nbytes: uint) {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let newMemory = bytearray_copy(
            evmCallFrame_getCalldata(topFrame),
            calldataOffset,
            evmCallFrame_getMemory(topFrame),
            memAddr,
            nbytes
        );
        if (evmCallStack_setTopFrameMemory(newMemory,)) {
            return;
        }
    }

    evm_runtimePanic(8);
}

public impure func evmOp_codesize() -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return account_getCodeSize(
            evmCallFrame_runningCodeFromAccount(topFrame)
        );
    } else {
        evm_runtimePanic(9);
        return 0;
    }
}

public impure func evmOp_codecopy(memAddr: uint, codeOffset: uint, nbytes: uint) {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let memory = evmCallFrame_getMemory(topFrame);
        if let Some(code) = account_getCode(
            evmCallFrame_runningCodeFromAccount(topFrame)
        ) {
            // if this would read beyond end of code,
            //       bytearray_copy will zero-fill the rest of the target memory region,
            //       which is the desired behavior
            let updatedMemory = bytearray_copy(
                code,
                codeOffset,
                memory,
                memAddr,
                nbytes
            );
            if (evmCallStack_setTopFrameMemory(updatedMemory,)) {
                return;
            }
        }
    }

    evm_runtimePanic(10);
}

public impure func evmOp_extcodesize(addrAsUint: uint) -> uint {
    let addr = address(addrAsUint);     // truncates if necessary
    if let Some(topFrame) = evmCallStack_topFrame() {
        return account_getCodeSize(
            evmCallFrame_getAccount(topFrame, addr)
        );
    }

    evm_runtimePanic(11);
    return 0;
}

public impure func evmOp_extcodecopy(addrAsUint: uint, memAddr: uint, codeOffset: uint, nbytes: uint) {
    let addr = address(addrAsUint);     // truncates if necessary
    if let Some(topFrame) = evmCallStack_topFrame() {
        let memory = evmCallFrame_getMemory(topFrame);
        if let Some(code) = account_getCode(
            evmCallFrame_getAccount(topFrame, addr)
        ) {
            // if this would read beyond end of code,
            //       bytearray_copy will zero-fill the rest of the target memory region,
            //       which is the desired behavior
            let updatedMemory = bytearray_copy(
                code,
                codeOffset,
                memory,
                memAddr,
                nbytes
            );
            if (evmCallStack_setTopFrameMemory(updatedMemory,)) {
                return;
            }
        } else {
            // code doesn't exist, so zero-fill the target memory region
            let updatedMemory = bytearray_copy(
                bytearray_new(0),
                0,
                memory,
                memAddr,
                nbytes
            );
            if (evmCallStack_setTopFrameMemory(updatedMemory,)) {
                return;
            }
        }
    }

    evm_runtimePanic(12);
}

public impure func evmOp_extcodehash(addrAsUint: uint) -> bytes32 {
    let addr = address(addrAsUint);     // truncates if necessary
    if let Some(topFrame) = evmCallStack_topFrame() {
        let acct = evmCallFrame_getAccount(topFrame, addr);
        if (! account_isEmpty(acct)) {
            return bytes32(0);
        }
        if let Some(code) = account_getCode(acct) {
            return keccak256(code, 0, bytearray_size(code));
        } else {
            return bytes32(0xc5d2460186f7233c927e7db2dcc703c0e500b653ca82273b7bfad8045d85a470);
        }
    }

    evm_runtimePanic(13);
    return bytes32(0);
}

public impure func evmOp_returndatasize() -> uint {
    if let Some(returnData) = evmCallStack_getTopFrameReturnData() {
        return bytearray_size(returnData);
    } else {
        return 0;
    }

    evm_runtimePanic(14);
    return 0;
}

public impure func evmOp_returndatacopy(memAddr: uint, dataOffset: uint, nbytes: uint) {
    let memory = evmCallStack_getTopFrameMemoryOrDie();
    if let Some(returnData) = evmCallStack_getTopFrameReturnData() {
        // if the returndata is smaller than nbytes,
        //       this will zero-fill the remainder of the target region,
        //       which is the desired behavior
        let updatedMemory = bytearray_copy(
            returnData,
            dataOffset,
            memory,
            memAddr,
            nbytes
        );
        if (evmCallStack_setTopFrameMemory(updatedMemory,)) {
           return;
        }
    } else {
        let updatedMemory = bytearray_copy(
            bytearray_new(0),
            0,
            memory,
            memAddr,
            nbytes
        );
        if (evmCallStack_setTopFrameMemory(updatedMemory,)) {
           return;
        }
    }

    evm_runtimePanic(15);
}

public impure func evmOp_timestamp() -> uint {
    return evmCallStack_currentTimestamp();
}

public impure func evmOp_number() -> uint {
    return evmCallStack_currentBlockNumber();
}

public impure func evmOp_mload(memAddr: uint) -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return bytearray_get256(
            evmCallFrame_getMemory(topFrame),
            memAddr
        );
    }

    evm_runtimePanic(16);
    return 0;
}

public impure func evmOp_mstore(memAddr: uint, value: uint) {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let memory = bytearray_set256(
            evmCallFrame_getMemory(topFrame),
            memAddr,
            value
        );
        if (evmCallStack_setTopFrameMemory(memory)) {
            return;
        }
    }

    evm_runtimePanic(17);
}

public impure func evmOp_mstore8(memAddr: uint, value:uint) {
    // debug((999, 1));
    if let Some(topFrame) = evmCallStack_topFrame() {
        // debug((999, 2));
        let memory = bytearray_setByte(
            evmCallFrame_getMemory(topFrame),
            memAddr,
            value
        );
        if (evmCallStack_setTopFrameMemory(memory)) {
            return;
        }
    }

    evm_runtimePanic(18);
}
    
public impure func evmOp_sload(storageAddr: uint) -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        if let Some(val) = account_getStorageCell(
            evmCallFrame_runningAsAccount(topFrame),
            storageAddr
        ) {
            return val;
        }
    }

    evm_runtimePanic(19);
    return 0;
}

public impure func evmOp_sstore(storageAddr: uint, value: uint) {
    if let Some(topFrame) = evmCallStack_topFrame() {
        if (evmCallFrame_shouldRevertOnStorageWrite(topFrame)) {
            // the currently running code was given a gas amount that wouldn't allow a storage write on EVM
            // for compatibility with EVM code that uses gas limit to prevent writes, we cause out-of-gas error here
            asm(1,) { setgas };
            let _ = asm(1,1) uint { plus };  // will get an out-of-gas error on this instruction
        } else {
            if let Some(_) = evmCallStack_setTopFrameStorageCell(
                storageAddr,
                value
            ) {
                return;
            }
        }
    }

    evm_runtimePanic(20);
}
    
public impure func evmOp_getjumpaddr(evm_pc: uint) -> impure func() {
    // This one isn't actually an EVM instruction.
    // It's a "pseudo-instruction" that translates an EVM jump destination to an AVM codepoint in the compiled code.
    if let Some(topFrame) = evmCallStack_topFrame() {
        if let Some(jumpTable) = account_getEvmJumpTable(
            evmCallFrame_runningCodeFromAccount(topFrame)
        ) {
            if let Some(codept) = jumpTable[evm_pc] {
                return codept;
            } else {
                evm_error();   // EVM code tried to jump to a forbidden EVM jump destination
                evmCallStack_callHitError(11);
                panic;
            }
        }
    }

    evm_runtimePanic(21);
    evmCallStack_callHitError(12);
    panic;
}

public impure func evmOp_msize() -> uint {
    if let Some(topFrame) = evmCallStack_topFrame() {
        return 32*((bytearray_size(evmCallFrame_getMemory(topFrame))+31)/32);
    } else {
        evm_runtimePanic(22);
        return 0;
    }
}

public impure func evmOp_log0(
    memAddr: uint, 
    nbytes: uint
) {
    let data = bytearray_extract(
        evmCallStack_getTopFrameMemoryOrDie(),
        memAddr,
        nbytes
    );
    if (evmCallStack_addEvmLogToCurrent(
        (
            address(0),  // addEvmLogToCurrent will fill this in
            bytearray_marshalFull(data),
        )
    ) == Some(()) ) {
        return;
    }

    evm_runtimePanic(23);
    panic;
}

public impure func evmOp_log1(
    memAddr: uint, 
    nbytes: uint, 
    topic0: uint
) {
    let data = bytearray_extract(
        evmCallStack_getTopFrameMemoryOrDie(),
        memAddr,
        nbytes
    );
    if (evmCallStack_addEvmLogToCurrent(
        (
            address(0),  // addEvmLogToCurrent will fill this in
            bytearray_marshalFull(data),
            topic0
        )
    ) == Some(()) ) {
        return;
    }

    evm_runtimePanic(24);
    panic;
}

public impure func evmOp_log2(
    memAddr: uint, 
    nbytes: uint, 
    topic0: uint,
    topic1: uint,
) {
    let data = bytearray_extract(
        evmCallStack_getTopFrameMemoryOrDie(),
        memAddr,
        nbytes
    );
    if (evmCallStack_addEvmLogToCurrent(
        (
            address(0),  // addEvmLogToCurrent will fill this in
            bytearray_marshalFull(data),
            topic0,
            topic1
        )
    ) == Some(()) ) {
        return;
    }

    evm_runtimePanic(25);
    panic;
}

public impure func evmOp_log3(
    memAddr: uint, 
    nbytes: uint, 
    topic0: uint,
    topic1: uint,
    topic2: uint,
) {
    let data = bytearray_extract(
        evmCallStack_getTopFrameMemoryOrDie(),
        memAddr,
        nbytes
    );
    if (evmCallStack_addEvmLogToCurrent(
        (
            address(0),  // addEvmLogToCurrent will fill this in
            bytearray_marshalFull(data),
            topic0,
            topic1,
            topic2
        )
    ) == Some(()) ) {
        return;
    }

    evm_runtimePanic(26);
    panic;
}

public impure func evmOp_log4(
    memAddr: uint, 
    nbytes: uint, 
    topic0: uint,
    topic1: uint,
    topic2: uint,
    topic3: uint,
) {
    let data = bytearray_extract(
        evmCallStack_getTopFrameMemoryOrDie(),
        memAddr,
        nbytes
    );
    if (evmCallStack_addEvmLogToCurrent(
        (
            address(0),  // addEvmLogToCurrent will fill this in
            bytearray_marshalFull(data),
            topic0,
            topic1,
            topic2,
            topic3
        )
    ) == Some(()) ) {
        return;
    }

    evm_runtimePanic(27);
    panic;
}

public impure func evmOp_callAndReturn(
    gas: uint,
    callee: address,
    balance: uint,
    argsOffset: uint,
    argsLength: uint,
    retOffset: uint,
    retLength: uint
) -> bool {
    let savedAuxStack = snapshotAuxStack();

    // remove first two items from saved aux stack, so we get our caller's aux stack state
    savedAuxStack = stack_discardDeepestItems(savedAuxStack, 2);

    // First, get the return address of our caller.
    // This will be the second-from-top item on the AVM AuxStack.
    // Get a copy of it, without disturbing anything.
    let resumeCodePoint = asm() func() {
        auxpop
        auxpop
        dup0
        swap2
        swap1
        auxpush
        auxpush
    };

    // Now make the call.
    // After the call completes, control will be thrown directly back to resumeCodePoint.
    let res = evmCallStack_doCall(
        0,
        gas,
        callee,
        balance,
        argsOffset,
        argsLength,
        retOffset,
        retLength,
        resumeCodePoint,
        Some(savedAuxStack),
    );
    if let Some(success) = res {
        return success;
    } else {
        return false;  // weird failure, best to return false
    }
}

public impure func evmOp_call(
    gas: uint,
    calleeAsUint: uint,
    balance: uint,
    argsOffset: uint,
    argsLength: uint,
    retOffset: uint,
    retLength: uint
) -> bool {
    let callee = address(calleeAsUint);  // truncates if necessary

    // First, get the return address of our caller.
    // This will be the second-from-top item on the AVM AuxStack.
    // Get a copy of it, without disturbing anything.
    let resumeCodePoint = asm() func() {
        auxpop
        auxpop
        dup0
        swap2
        swap1
        auxpush
        auxpush
    };

    // Now make the call.
    // After the call completes, control will be thrown directly back to resumeCodePoint.
    let res = evmCallStack_doCall(
        0,
        gas,
        callee,
        balance,
        argsOffset,
        argsLength,
        retOffset,
        retLength,
        resumeCodePoint,
        None<Stack>,
    );
    if let Some(success) = res {
        return success;
    } else {
        return false;  // weird failure, best to return false
    }
}

public impure func evmOp_callcode(
    gas: uint,
    calleeAsUint: uint,
    balance: uint,
    argsOffset: uint,
    argsLength: uint,
    retOffset: uint,
    retLength: uint
) -> bool {
    let callee = address(calleeAsUint);     // truncates if necessary

    // This is the same as evmOp_call, except for the callKind.
    let resumeCodePoint = asm() func() {
        auxpop
        auxpop
        dup0
        swap2
        swap1
        auxpush
        auxpush
    };
    let res = evmCallStack_doCall(
        1,
        gas,
        callee,
        balance,
        argsOffset,
        argsLength,
        retOffset,
        retLength,
        resumeCodePoint,
        None<Stack>,
    );
    if let Some(success) = res {
        return success;
    } else {
        return false;  // weird failure, best to return false
    }
}

public impure func evmOp_delegatecall(
    gas: uint,
    calleeAsUint: uint,
    argsOffset: uint,
    argsLength: uint,
    retOffset: uint,
    retLength: uint
) -> bool {
    let callee = address(calleeAsUint);     // truncates if necessary

    // This is the same as evmOp_call, except for the callKind.
     let resumeCodePoint = asm() func() {
         auxpop
         auxpop
         dup0
         swap2
         swap1
         auxpush
         auxpush
     };
     let res = evmCallStack_doCall(
         2,
         gas,
         callee,
         0,  // balance will be filled in by evmCallStack_doCall
         argsOffset,
         argsLength,
         retOffset,
         retLength,
         resumeCodePoint,
         None<Stack>,
     );
    if let Some(success) = res {
        return success;
    } else {
        return false;  // weird failure, best to return false
    }
}

public impure func evmOp_staticcall(
    gas: uint,
    calleeAsUint: uint,
    argsOffset: uint,
    argsLength: uint,
    retOffset: uint,
    retLength: uint
) -> bool {
    let callee = address(calleeAsUint);     // truncates if necessary

    // This is the same as evmOp_call, except for the callKind.
    let resumeCodePoint = asm() func() {
        auxpop
        auxpop
        dup0
        swap2
        swap1
        auxpush
        auxpush
    };
    let res = evmCallStack_doCall(
        3,
        gas,
        callee,
        0,  // balance is zero for a staticcall
        argsOffset,
        argsLength,
        retOffset,
        retLength,
        resumeCodePoint,
        None<Stack>,
    );
    if let Some(success) = res {
        return success;
    } else {
        return false;  // weird failure, best to return false
    }
}

public impure func evmOp_revert(memOffset: uint, memNbytes: uint) {
    let _ = evmCallStack_returnFromCall(false, memOffset, memNbytes);

    evm_runtimePanic(28);
}

public impure func evmOp_revert_knownCodePc(codeAddr: address, evmPc: uint, memOffset: uint, memNbytes: uint) {
    asm((10000, codeAddr, evmPc),) { debugprint };
    evmOp_revert(memOffset, memNbytes);
}

public impure func evmOp_revert_knownPc(evmPc: uint, memOffset: uint, memNbytes: uint) {
    let codeAddr = address(0);
    if let Some(acct) = evmCallStack_runningCodeFromAccount() {
        codeAddr = account_getAddress(acct);
    }
    evmOp_revert_knownCodePc(codeAddr, evmPc, memOffset, memNbytes);
}

public impure func evmOp_return(memOffset: uint, memNbytes: uint) {
    let _ = evmCallStack_returnFromCall(true, memOffset, memNbytes);

    evm_runtimePanic(29);
}

public impure func evmOp_selfdestruct(ownerAsUint: uint) {
    let owner = address(ownerAsUint);     // truncates if necessary

    evmCallStack_selfDestructCurrentAccount(owner,);
    evmOp_return(0, 0);
}

public impure func evmOp_create(value: uint, offset: uint, length: uint) -> address {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let myAcct = evmCallFrame_runningAsAccount(topFrame);
        let myAddr = account_getAddress(myAcct);
        let (seqNum, updatedAcct) = account_fetchAndIncrSeqNum(myAcct);
        if ( ! evmCallStack_setAccount(myAddr, updatedAcct)) {
            return address(0);
        }
        let newAddress = address(keccakOfRlpEncodedUintPair(uint(myAddr), seqNum));
        return doCreationOpcode(value, offset, length, newAddress);
    }

    evm_runtimePanic(30);
    panic;
}

public impure func evmOp_create2(value: uint, offset: uint, length: uint, salt: uint) -> address {
    if let Some(topFrame) = evmCallStack_topFrame() {
        let myAcct = evmCallFrame_runningAsAccount(topFrame);
        let myAddr = account_getAddress(myAcct);
        let newAddrBuf = bytearray_new(85);
        newAddrBuf = bytearray_setByte(newAddrBuf, 0, 0xff);
        newAddrBuf = bytearray_set256(newAddrBuf, 1, asm(12*8, myAddr) uint { shl });
        newAddrBuf = bytearray_set256(newAddrBuf, 1+20, salt);
        newAddrBuf = bytearray_set256(
            newAddrBuf,
            1+20+32,
            uint(keccak256(
                evmCallFrame_getMemory(topFrame),
                offset,
                length
            ))
        );
        let newAddress = address(keccak256(newAddrBuf, 0, 85));
        return doCreationOpcode(value, offset, length, newAddress);
    }

    evm_runtimePanic(31);
    panic;
}

impure func doCreationOpcode(value: uint, offset: uint, length: uint, newAddress: address) -> address {
    // make sure there isn't already an account at the given address
    if let Some(acct) = evmCallStack_getAccount(newAddress) {
        if (account_hasContract(acct)) {
            // there is already a contract at that address; return failure
            return address(0);
        }
    } else {
        // somehow there isn't an EVM callframe
        evm_runtimePanic(32);
        panic;
    }

    let constructorCode = bytearray_extract(
        evmCallStack_getTopFrameMemoryOrDie(),
        offset,
        length
    );
    if let Some(res) = translateEvmCodeSegment(bytestream_new(constructorCode), false) {
        let (startPoint, evmJumpTable, _) = res;

        // Create a new account to run the constructor code.
        if (evmCallStack_setAccount(
                newAddress,
                account_setContractInfo(
                    pristineAccount(newAddress),
                    constructorCode,
                    evmJumpTable,
                    startPoint,
                    storageMap_new(),
                    true,
                )
            )
        ) {
            // Now for the slightly tricky part.
            // We're going to call the untrusted constructor, which might end up polluting our
            //    Mini-language callstack (which is stored on the AVM auxstack).
            // The possibly-polluted callstack includes our local variables and our return address.
            // It also contains the locals and return address of our caller, which we also need to preserve.
            // We don't need to preserve any more because our caller's caller is translated EVM code,
            //    which doesn't use the aux stack and isn't trusted to maintain any invariants about it.
            // We will make a copy of the things we want to preserve, on the AVM stack, because the AVM stack
            //    gets saved and restored across calls to untrusted code.
            // After the return we'll restore the saved info, and continue.

            // copy top four AVM auxstack items onto the AVM stack (locals and return address of us and our caller)
            asm(((),(),(),()),) {
                auxpop
                swap1
                auxpop
                swap1
                auxpop
                swap1
                auxpop
                swap1
                dup1
                auxpush
                [0] tset
                dup1
                auxpush
                [1] tset
                dup1
                auxpush
                [2] tset
                dup1
                auxpush
                [3] tset
            };

            let constructorSucceeded = false;

            // We can't save the result of this call to a local variable, because when the calls returns,
            //     we can't (yet) trust that our local callframe is in a reasonable state.
            //     So we have to use an if-statement to capture the return value.
            if (evmOp_call(
                1000000000,  // gas allocation
                uint(newAddress),
                value,
                0,           // no calldata passed to constructor
                0,
                0,           // don't copy returndata back into caller's memory
                0,
            )) {
                // Constructor call succeeded.
                // Recover our saved information from the stack, put it back onto the auxstack
                asm() {
                    dup0
                    [0] tget
                    auxpush
                    dup0
                    [1] tget
                    auxpush
                    dup0
                    [2] tget
                    auxpush
                    [3] tget
                    auxpush
                };

                constructorSucceeded = true;
            } else {
                // Recover our saved information from the stack, put it back onto the auxstack
                asm() {
                    dup0
                    [0] tget
                    auxpush
                    dup0
                    [1] tget
                    auxpush
                    dup0
                    [2] tget
                    auxpush
                    [3] tget
                    auxpush
                 };
            }

            if (constructorSucceeded) {
                if let Some(contractCode) = evmCallStack_getTopFrameReturnData() {
                    if let Some(res) = translateEvmCodeSegment(bytestream_new(contractCode), false) {
                        let (startPoint2, evmJumpTable2, _) = res;
                        if let Some(oldAcct) = evmCallStack_getAccount(newAddress) {
                            let storage = storageMap_new();
                            if let Some(st) = account_getAllStorage(oldAcct) {
                                storage = st;
                            }
                            if (evmCallStack_setAccount(
                                    newAddress,
                                    account_setContractInfo(
                                        oldAcct,
                                        contractCode,
                                        evmJumpTable2,
                                        startPoint2,
                                        storage,
                                        true,
                                    )
                                )
                            ) {
                                return newAddress;
                            }
                        }
                    }
                }
            }
        }
    }

    // clean up the state and return failure
    let _ = evmCallStack_setAccount(newAddress, pristineAccount(newAddress));
    return address(0);
}

impure func evm_error() {
    // This should be called when EVM code has generated an EVM error.
    evmOp_revert_knownPc(0xffffffffffffffff, 0, 0);  // should never return

}

impure func evm_runtimePanic(reasonCode: uint) {
    // This should be called when something that "shouldn't ever happen" has occurred.
    // It should only be called if something has gone wrong in the trusted code.
    // If untrusted code has encountered an error, that will be handled elsewhere.
    evmCallStack_callHitError(1000+reasonCode);
    panic;
}

