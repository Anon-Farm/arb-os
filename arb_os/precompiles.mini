//
// Copyright 2020, Offchain Labs, Inc. All rights reserved.
//

import type EvmCallFrame;
import type AccountStore;
import type ByteArray;

import impure func evmCallStack_topFrame() -> option<EvmCallFrame>;
import impure func evmCallStack_setTopFrameMemory(mem: ByteArray) -> bool;
import func evmCallFrame_getCalldata(frame: EvmCallFrame) -> ByteArray;

import impure func evmOp_return(offset: uint, nbytes: uint);
import impure func evmOp_revert(offset: uint, nbytes: uint);

import func getGlobalAccountStore() -> AccountStore;
import func setGlobalAccountStore(acctStore: AccountStore);
import func accountStore_createBuiltinContract(
    acctStore: AccountStore,
    addr: address,
    entryPoint: impure func(),
    isArbitrumStyle: bool
) -> option<AccountStore>;

import func bytearray_new(capacity: uint) -> ByteArray;
import func bytearray_size(ba: ByteArray) -> uint;
import func bytearray_getByte(ba: ByteArray, offset: uint) -> uint;
import func bytearray_get256(ba: ByteArray, offset: uint) -> uint;
import func bytearray_set256(ba: ByteArray, offset: uint, val: uint) -> ByteArray;

import func sha256_byteArray(ba: ByteArray) -> bytes32;

import impure func arbsys_txcall();


public impure func precompiles_init(acctStore: AccountStore) -> option<AccountStore> {
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x01),
        precompile_0x01,
        false,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x02),
        precompile_0x02,
        false,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x06),
        precompile_0x06,
        false,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x07),
        precompile_0x07,
        false,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(0x08),
        precompile_0x08,
        false,
    )?;
    acctStore = accountStore_createBuiltinContract(
        acctStore,
        address(100),
        arbsys_txcall,
        true,
    )?;
    return Some(acctStore);
}

public impure func precompile_0x01() {  // ecrecover
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) != 4*32) {
            evmOp_revert(0, 0);
        }
        let h = bytearray_get256(calldata, 0);
        let v = bytearray_get256(calldata, 32);
        let r = bytearray_get256(calldata, 2*32);
        let s = bytearray_get256(calldata, 3*32);
        let addr = asm(r, s, v-27, h) address { ecrecover };

        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(bytearray_new(0), 0, uint(addr))
        );
        if (success) {
            evmOp_return(0, 32);
        }
    }

    evmOp_revert(0, 0);
}

public impure func precompile_0x02() {  // sha256
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let result = sha256_byteArray(calldata);
        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(bytearray_new(0), 0, uint(result))
        );
        if (success) {
            evmOp_return(0, 32);
        }
    }

    evmOp_revert(0, 0);
}

public impure func precompile_0x06() {  // ecadd
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) != 4*32) {
            evmOp_revert(0, 0);
        }
        let x0 = bytearray_get256(calldata, 0);
        let x1 = bytearray_get256(calldata, 32);
        let y0 = bytearray_get256(calldata, 2*32);
        let y1 = bytearray_get256(calldata, 3*32);

        let z0 = asm(x0, x1, y0, y1) uint { ecadd };
        let z1 = asm() uint { };

        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    bytearray_new(0),
                    0,
                    z0,
                ),
                32,
                z1,
            )
        );
        if (success) {
            evmOp_return(0, 64);
        }
    }

    evmOp_revert(0, 0);
}

public impure func precompile_0x07() {  // ecmul
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        if (bytearray_size(calldata) != 3*32) {
            evmOp_revert(0, 0);
        }
        let x0 = bytearray_get256(calldata, 0);
        let x1 = bytearray_get256(calldata, 32);
        let n = bytearray_get256(calldata, 2*32);

        let z0 = asm(x0, x1, n) uint { ecmul };
        let z1 = asm() uint { };

        let success = evmCallStack_setTopFrameMemory(
            bytearray_set256(
                bytearray_set256(
                    bytearray_new(0),
                    0,
                    z0,
                ),
                32,
                z1,
            )
        );
        if (success) {
            evmOp_return(0, 64);
        }
    }

    evmOp_revert(0, 0);
}

public impure func precompile_0x08() { // ecpairing
    if let Some(topFrame) = evmCallStack_topFrame() {
        let calldata = evmCallFrame_getCalldata(topFrame);
        let numPts = bytearray_size(calldata) / (6*32);
        if ((numPts > 0) && (numPts <= 30)) {
            let acc = unsafecast<any>(());
            let offset = 0;
            while (numPts > 0) {
                acc = (
                    (
                        bytearray_get256(calldata, offset),
                        bytearray_get256(calldata, offset+32),
                        bytearray_get256(calldata, offset+3*32),
                        bytearray_get256(calldata, offset+2*32),
                        bytearray_get256(calldata, offset+5*32),
                        bytearray_get256(calldata, offset+4*32)
                    ),
                    acc
                );
                offset = offset + 6*32;
                numPts = numPts - 1;
            }

            let result = asm(acc,) bool { ecpairing };
            let success = evmCallStack_setTopFrameMemory(
                bytearray_set256(
                    bytearray_new(0),
                    0,
                    uint(result)
                )
            );
            if (success) {
                evmOp_return(0, 32);
            }
        }
    }
}
